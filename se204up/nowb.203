"make sure classes needed by this package exist"
#( 
    DialogTopPane UserDLL WinAddress WinDialogInfo WindowHandle 
    WinLong 
)
do: [ :className |
     Smalltalk at: className ifAbsent: [
        ^self error: className asString, ' is needed to install this package.'
    ]
];
yourself!

  
"Non-WindowBuilder Fixes for Smalltalk Express v2.0

    Description:

        This package corrects several base image issues in a Smalltalk Express v2.0 image
        that does not contain WindowBuilder and updates the image to v2.03. These fixes have
        already been folded into the current version of WindowBuilder.

    Author: David Zeleznik, ObjectShare, Inc.
    Date: 2/1/97

    Details and Original Digitalk Problem Report #'s:

        *  PR#1344 open WindowDialogs eventually causes GPF.
"!

!DialogTopPane methods !
  
buildWindow: aParentWindow
        "Private - Create the dialog window for the receiver."
    | winDlgTemplate items winDlgFontInfo tempByteArray m pDlgTemplate |

    style isNil ifTrue: [style := self defaultFrameStyle].
    framingBlock isNil
        ifTrue: [
            rectangle := self initSize.
            framingBlock := rectangle]
        ifFalse: [rectangle := framingBlock value: parent rectangle].

    winDlgTemplate := WinDialogInfo
        dialogTemplateStyle: style
        itemCount: 0
        x: rectangle origin x
        y: rectangle origin y
        cx: rectangle extent x
        cy: rectangle extent y
        menuName: nil
        className: nil
        text: label.

    winDlgFontInfo := WinDialogInfo         "not used yet"
        fontPointSize: 0 typeFaceName: nil.

    items := self buildItems.
    winDlgTemplate byteAtOffset: 4 put: items size.

    tempByteArray := winDlgTemplate contents.
    items do: [:i |
        tempByteArray := tempByteArray, i contents].
    winDlgTemplate contents: tempByteArray.

    pDlgTemplate := WinAddress copyToNonSmalltalkMemory: winDlgTemplate contents.
    handle := WindowHandle fromInteger: (
        UserLibrary
            createDialogIndirect: WIN hInstanceExe
            dialogTemplate: pDlgTemplate asParameter
            parent: parent asParameter
            dialogFunction:
                (WinLong fromBytes: DialogProcInstance) asParameter).
    pDlgTemplate free.

    handle = NullHandle ifTrue: [^self error: 'Dialog creation error'].
    self initGraphics.
    self receiveMessages.
    self buildDialogItems.
    children do: [:subpane | subpane validate].

    m := UserLibrary
            getSystemMenu: self asParameter
            revert: 0.
    self propertyAt: #sysMenu put: m.
    PoppedModelessWindows add: handle.
    self showWindow! !

!UserDLL methods !
 
createDialogIndirect: hInstance dialogTemplate: lpDialogTemplate parent: hWndParent
dialogFunction: lpDialogFunc
    <api: CreateDialogIndirect ushort ulong ushort ulong ushort>
    ^self invalidArgument! !


"construct package" | pkgClass package |
pkgClass := Smalltalk at: #XoPackage ifAbsent: [ ^nil ].
package := pkgClass named: 'Version 2.03 Non-WindowBuilder Fixes'.
package makeCurrent;
    organize: DialogTopPane methodsFor: '' include: #( 
        buildWindow: 
    );
    organize: UserDLL methodsFor: '' include: #( 
        createDialogIndirect:dialogTemplate:parent:dialogFunction: 
    );
    postPackageFiles;
    integratesClasses: true;
    sharesBehaviors: true;
    fileSpec: 'NOWB.203';
    comments:
'"Non-WindowBuilder Fixes for Smalltalk Express v2.0

    Description:

        This package corrects several base image issues in a Smalltalk Express v2.0 image
        that does not contain WindowBuilder and updates the image to v2.03. These fixes have
        already been folded into the current version of WindowBuilder.

    Author: David Zeleznik, ObjectShare, Inc.
    Date: 2/1/97

    Details and Original Digitalk Problem Report #''s:

        *  PR#1344 open WindowDialogs eventually causes GPF.
"';
    fileTime: ( Time fromSeconds: 43321 )
          on: ( Date fromDays: 35099 );
    takeCurrent;
    yourself   !
